%skeleton "lalr1.cc"
%require "3.7"
%language "c++"
%defines
%define api.namespace {hse::model}
%define api.parser.class {CustomParser}
%define api.token.raw
%define api.value.type variant
%define parse.trace
%debug

%code requires{
    #include "json_model.hpp"
    #include <iostream>
    #include <string>
    #include <variant>
    namespace hse::model{
		class CustomDriver;
		class CustomScanner;
	}
}

%parse-param {CustomScanner& scanner}
%parse-param {CustomDriver& driver}

%locations

%code {

    #include "CustomDriver.hpp"
    #undef yylex
    #define yylex scanner.yylex   
}

%token END 0
%token <std::int64_t> INT
%token <double> FLOAT
%token <std::string> STRING
%token <bool> TRUE_ FALSE_
%token <std::monostate> null
%left M_BEGIN M_END S_BEGIN S_END
%left COMMA
%left COLON
%type <hse::model::map_t> MAP
%type <hse::model::map_t> PAIR
%type <hse::model::map_t> MEMBERS
%type <hse::model::seq_t> SEQ
%type <hse::model::seq_t> ELEMENTS
%type <hse::model::node> VALUE

%%

START: MAP {
    driver.result = json($1);
  }
;
MAP: M_BEGIN M_END {
    $$ = map_t {};
  }
| M_BEGIN MEMBERS M_END {
    $$ = $2;
  }
;
MEMBERS: PAIR {
    $$ = $1;
  }
|  MEMBERS COMMA PAIR {
    $1.merge($3);
    $$ = $1;
  }
;
PAIR: STRING COLON VALUE {
    $$ = map_t{{$1,$3}};
  }
;
SEQ: S_BEGIN S_END {
    $$ = seq_t {};
  }
| S_BEGIN ELEMENTS S_END {
    $$ = $2;
}
;
ELEMENTS: VALUE {
    $$ = seq_t{$1};
  }
|  ELEMENTS COMMA VALUE {
    $1.push_back( $3 );
    $$ = $1;
  }
;
VALUE: 
  STRING {$$=node($1);}
| INT {$$=node($1);}
| FLOAT {$$=node($1);}
| MAP {$$=node($1);}
| SEQ {$$=node($1);}
| TRUE_ {$$=node(true);}
| FALSE_ {$$=node(false);}
| null {$$=node{};}
;

%%

